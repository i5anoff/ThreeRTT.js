<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8">
  <title>Raytracer</title>

  <script type="text/javascript" src="domready.js"></script>
  <script type="text/javascript" src="tquery-bundle.js"></script>
  <script type="text/javascript" src="ThreeBox.js"></script>
  <script type="text/javascript" src="../build/ThreeRTT-tquery.js"></script>

  <style type="text/css" media="screen">
    html, body {
      height: 100%;
    }
  </style>

  <script type="application/x-glsl" charset="utf-8" id="rt-test">
    uniform vec2 cameraViewport;
    uniform mat4 cameraWorld;
    uniform vec3 shiftOffset;
    uniform float shiftAngle;

    varying vec2 vUV;

    // geometrical transforms

    vec4 fold(vec4 z) {
      return vec4(2.*clamp(z.xyz, -1., 1.) - z.xyz, z.w);
    }

    vec4 warp(vec4 z) {
      float th = cos(clamp(z.y, -1., 1.) * 3.14) * .1 + .1;
      float c = cos(th);
      float s = sin(th);
      z.xz *= mat2(c, s, -s, c);
      z.w *= 1.1;
      return z;
    }

    vec4 rotate(vec4 z, float th) {
      float c = cos(th);
      float s = sin(th);
      z.xz *= mat2(c, s, -s, c);
      return z;
    }

    vec4 affine(vec4 z, float factor, vec3 offset) {
      z.xyz *= factor * vec3(-1., -1., -1.);
      z.xyz += offset;
      z.w *= abs(factor);
      return z;
    }

    vec4 mandel(vec4 z, vec3 offset) {
      float x = z.x;
      float y = z.y;

      z.w = 2. * length(z.xy) * z.w + 1.;

      z.x = x*x - y*y + offset.x;
      z.y = 2.*x*y + offset.y;

      return z;
    }

    vec4 invert(vec4 z, float factor) {
      float r2 = dot(z.xyz, z.xyz);
      float f = factor / r2;
      return z * f;
    }

    vec4 invertRadius(vec4 z, float radius2, float limit) {
      float r2 = dot(z.xyz, z.xyz);
      float f = clamp(radius2 / r2, 1., limit);
      return z * f;
    }

    // solids

    float plane(vec3 pos, vec3 normal, float offset) {
      return dot(pos, normal) - offset;
    }

    float box(vec3 pos, float radius) {
      vec3 u = abs(pos);
      return max(max(u.x, u.y), u.z) - radius;
    }

    float sphere(vec3 pos, float radius) {
      return length(pos) - radius;
    }

    float torus(vec2 pos1, float pos2, float radius1, float radius2) {
      float r = length(pos1) - radius1;
      return (length(vec2(r, pos2)) - radius2);
    }

    float cross2(vec2 pos, float radius) {
      return length(pos) - radius;
    }

    float cross3(vec3 pos, float radius) {
      return min(min(cross2(pos.xy, radius), cross2(pos.xz, radius)), cross2(pos.yz, radius));
    }

    // distance estimator func

    float distance(vec3 pos) {
      vec3 offset = pos;
      vec4 z = vec4(pos, 1.);

      // hybrid fractal folds

      z = fold(z);
      z = invertRadius(z, .5, 2.);
      z = affine(z, 2.5, offset);
      z.w += 1.;

      z = rotate(z, shiftAngle);

      z = fold(z);
      z = invert(z, 1.2);

      z = fold(z);
      z = invert(z, 1.1);

      z = fold(z);
      z = invert(z, .7);

      z = affine(z, 2.5, offset);
      z.w += 1.;

      z.xyz += shiftOffset;

      z = fold(z);
      z = rotate(z, shiftAngle * 3.1 + 1.34);

      z = fold(z);
      z = rotate(z, shiftAngle * 1.7 + 2.43);

      return min(
        cross2(z.xz, .1),
        box(z.xyz, .25)
      ) / z.w * .96;
    }

    void main() {

      // raytracing set up
      vec2 xy = -(vUV * 2. - 1.) * cameraViewport * .5;
      vec3 ray = normalize((cameraWorld * vec4(xy, 1., 1.)).xyz);
      vec3 pos = -(cameraWorld * vec4(0, 0, 0., 1.)).xyz;

      float inv = 1.;
      float y = distance(pos);
      float travel = 0.;

      float step = 1., y2, dy, inv2, occ = 1.;

      // ray marching
      for (int i = 0; i < 32; ++i) {

        if (abs(step) < .00001) {
          break;
        }

        step = y * inv;
        pos += ray * step;
        travel += step;
        y = distance(pos);
        occ += .01;

      }

      // coloring
      vec3 blue = vec3(.2, .5, .9);
      vec3 green = vec3(.2, .9, .2);
      float cf = travel;
      vec3 color = blue + (green - blue) * travel;

      // normal for shading
      float epsilon = .001;
      float base = y;
      vec3 norm = normalize(vec3(
          distance(pos + vec3(epsilon, 0, 0)),
          distance(pos + vec3(0, epsilon, 0)),
          distance(pos + vec3(0, 0, epsilon))
        ) - base);

      // ambient occlusion
      float ao = 0.2, radius = .003;
      for (int i = 0; i < 4; ++i) {
        pos += norm * radius;
        ao += (distance(pos) - base) / radius;
        radius *= 2.;
      }
      ao *= .25;

      // shading + fog
      float diffuse = (dot(norm, normalize(vec3(5., 2., 1.))) * .5 + .5);
      float fog = min(1., max(0., exp(-travel * 2.2) * 2.2));

      // gamma correct lighting
      gl_FragColor = vec4(sqrt(
        0.05 * color * fog * ao * (diffuse * .5 + .5) +
        color * max(0., 1.0 - fog)
      ), 1.0);
    }
  </script>

  <script type="text/javascript" charset="utf-8">

    DomReady.ready(function () {
      ThreeBox.preload([
        '../build/ThreeRTT.glsl.html',
      ], function (assets) {

        // init threebox
        var world = window.world = threeBox({
          cameraControls: true,
          orbit: 1.5,
          theta: 0,
          phi: Ï„/4,
        }).start();

        // raytracing parameters
        var opts = {
          width: 512,
          height: 512,
        };

        // uniforms for animation
        var uniforms = {
          shiftAngle: {
            type: 'f',
            value: 0,
          },
          shiftOffset: {
            type: 'v3',
            value: new THREE.Vector3(),
          },
        };

        // set up raytrace pass
        var raytrace = window.raytrace = world.rtt(opts).raytrace('rt-test', {}, uniforms);
        var camera = raytrace.tCamera();

        world.loop().hookPreRender(function () {
          var th = +new Date() * .0005;

          // animate parameters
          var v = uniforms.shiftOffset.value;
          v.x = Math.cos(th) * .5;
          v.y = Math.sin(th * .124 + v.x) * .5;
          v.z = Math.sin(th * .357 + v.y + v.x) * .5;
          uniforms.shiftAngle.value = (th * .5 + Math.cos(th * .11)) * .1 % (Math.PI * 2);

          if (!window.tt) {
            // reset camera controls
            world._cameraControls.orbit = .47;
            world._cameraControls.theta = .8;
          }
          window.tt = true;
        })

        // bind camera controls to raytracer
        world._cameraControls.camera = raytrace.tCamera();

        // display raytrace result
        world.display([ raytrace ], 1);

      });
    });

  </script>

</head>
<body>
  <div style="display: none"><img src="raytrace-creepy.png"></div>  
</body>
</html>
